`timescale 1ns / 1ps

module uart_tx_partial_scoap #(
    parameter CLKS_PER_BIT = 16
)(
    input            clk,
    input            rst,
    input      [7:0] tx_data,
    input            tx_valid,
    input            scan_en,
    input            scan_in,

    output           tx_ready,
    output           uart_tx,
    output           scan_out
);

    localparam COUNT_WIDTH =
        (CLKS_PER_BIT > 1) ? $clog2(CLKS_PER_BIT) : 1;

    localparam TOTAL_FF    = 8 + 3 + COUNT_WIDTH + 2 + 1;
    localparam SCAN_LENGTH = 2 + COUNT_WIDTH;

    localparam [1:0]
        IDLE  = 2'b00,
        START = 2'b01,
        DATA  = 2'b10,
        STOP  = 2'b11;

    wire [7:0]             data_reg;
    wire [2:0]             bit_index;
    wire [COUNT_WIDTH-1:0] clk_count;
    wire [1:0]             state;

    reg [7:0]             data_next;
    reg [2:0]             bit_index_next;
    reg [COUNT_WIDTH-1:0] clk_count_next;
    reg [1:0]             state_next;
    reg                   uart_tx_next;

    wire [TOTAL_FF-1:0]    scan_d;
    wire [TOTAL_FF-1:0]    scan_q;
    wire [SCAN_LENGTH:0]   scan_link;

    // Logic chuc nang UART TX.
    always @(*) begin
        data_next      = data_reg;
        bit_index_next = bit_index;
        clk_count_next = clk_count;
        state_next     = state;
        uart_tx_next   = uart_tx;

        case (state)
            IDLE: begin
                uart_tx_next   = 1'b1;
                clk_count_next = 0;
                bit_index_next = 0;

                if (tx_valid && tx_ready) begin
                    data_next    = tx_data;
                    uart_tx_next = 1'b0;
                    state_next   = START;
                end
            end

            START: begin
                if (clk_count != CLKS_PER_BIT - 1) begin
                    clk_count_next = clk_count + 1'b1;
                end
                else begin
                    clk_count_next = 0;
                    uart_tx_next   = data_reg[0];
                    state_next     = DATA;
                end
            end

            DATA: begin
                if (clk_count != CLKS_PER_BIT - 1) begin
                    clk_count_next = clk_count + 1'b1;
                end
                else begin
                    clk_count_next = 0;

                    if (bit_index < 7) begin
                        bit_index_next = bit_index + 1'b1;
                        uart_tx_next   = data_reg[bit_index + 1];
                    end
                    else begin
                        uart_tx_next = 1'b1;
                        state_next   = STOP;
                    end
                end
            end

            STOP: begin
                if (clk_count != CLKS_PER_BIT - 1) begin
                    clk_count_next = clk_count + 1'b1;
                end
                else begin
                    clk_count_next = 0;
                    state_next     = IDLE;
                end
            end

            default: begin
                // Giu nguyen gia tri nhu UART TX goc.
            end
        endcase
    end

    genvar i;
    generate
        for (i = 0; i < TOTAL_FF; i = i + 1) begin : GEN_FF

            if ((i == 8) || (i == 9) ||
                ((i >= 11) && (i < 11 + COUNT_WIDTH))) begin : SCANNED

                // Anh xa vi tri FF sang vi tri trong chuoi scan.
                // i=8,9 -> 0,1; i=11,12,... -> 2,3,...
                localparam SCAN_INDEX =
                    (i < 11) ? (i - 8) : (i - 9);

                scan_dff #(
                    .RESET_VALUE(1'b0)
                ) u_scan_dff (
                    .clk     (clk),
                    .rst     (rst),
                    .scan_en (scan_en),
                    .d_in    (scan_d[i]),
                    .scan_in (scan_link[SCAN_INDEX]),
                    .q       (scan_q[i])
                );

                assign scan_link[SCAN_INDEX + 1] = scan_q[i];

            end
            else begin : NON_SCANNED

                reg q_reg;

                // FF khong scan van cap nhat theo logic chuc nang
                // trong cac chu ky shift.
                always @(posedge clk) begin
                    if (rst)
                        q_reg <= (i == TOTAL_FF - 1)
                                 ? 1'b1 : 1'b0;
                    else
                        q_reg <= scan_d[i];
                end

                assign scan_q[i] = q_reg;

            end
        end
    endgenerate

    assign scan_d = {
        uart_tx_next,
        state_next,
        clk_count_next,
        bit_index_next,
        data_next
    };

    assign {
        uart_tx,
        state,
        clk_count,
        bit_index,
        data_reg
    } = scan_q;

    assign tx_ready = (state == IDLE);

    assign scan_link[0] = scan_in;
    assign scan_out     = scan_link[SCAN_LENGTH];

endmodule